plugins {
    id "com.jfrog.bintray" version "1.7.3"
}

apply plugin: "java"
apply plugin: "idea"
apply plugin: "maven-publish"

group 'camelot'
version '1.0'

sourceCompatibility = 1.8

repositories {
    jcenter()
    mavenCentral()
}

dependencies {
    compile gradleApi()
}

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}

task sourcesJar(type: Jar) {
    from sourceSets.main.allSource
    classifier = 'sources'
}

def pomConfig = {

    licenses {
        license {
            name "The Apache Software License, Version 2.0"
            url "http://www.apache.org/licenses/LICENSE-2.0.txt"
            distribution "repo"
        }
    }

    developers {
        developer {
            id "trumpets"
            name "Ivo Neskovic"
            email "ivo.neskovic@gmail.com"
        }
    }

    scm {
        url "https://github.com/trumpets/merlin/"
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            artifact sourcesJar
            artifact javadocJar

            pom.withXml {
                def root = asNode()
                root.appendNode('description', 'A test” Gradle plugin')
                root.children().last() + pomConfig
            }
        }
    }
}

bintray {
    user = bintrayUser
    key = bintrayKey
    publications = ['mavenJava']

    publish = true
    override = true

    pkg {
        repo = 'Camelot'
        name = 'merlin-gradle'
        desc = "Some testing”"
        licenses = ['Apache-2.0']
        vcsUrl = 'https://github.com/trumpets/merlin.git'

        version {
            name = "1.0"
            released  = new Date()
        }
    }
}
